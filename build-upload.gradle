//库工程上传本地仓库////////////////////////////////////////////////////////////////////////////////////
//1.配置本地仓库路径和版本: gradle-local.properties
//      mvn_version=5.1.170407.1121
//      mvn_local_repo=file:C:/m2repository/repository
//2.Gradle执行: uploadArchives

//其他工程引入依赖////////////////////////////////////////////////////////////////////////////////////
//1.添加仓库: maven{ url 'file:C:/m2repository/repository'}
//2.添加依赖: compile 'sviolet:turquoise:version'

//给库模块加入上传任务
configure(subprojects.findAll { it.name.startsWith('turquoise') }) {

    apply plugin: 'maven'

    //编译时产生源代码jar
    task generateSourcesJar(type: Jar) {
        from android.sourceSets.main.java.srcDirs
        classifier 'sources'
    }

    //将源代码jar加入artifacts
    artifacts {
        archives generateSourcesJar
    }

    //上传artifacts
    uploadArchives {
        repositories.mavenDeployer {
            //暂时使用绝对路径指定本地仓库
            repository(url: uri(rootProject.ext.upload_repositoryPath))
            pom.project {
                groupId 'sviolet'
                artifactId project.name
                name project.name
                version rootProject.ext.upload_mavenVersion
                packaging 'aar'
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
                developers {
                    developer {
                        id 'shepherdviolet'
                        name 'Shepherd Violet'
                    }
                }
            }
        }
    }

}