//库工程上传本地仓库////////////////////////////////////////////////////////////////////////////////////

//1.配置本地仓库路径: gradle.properties -> local_repo_path=C:/m2repository/repository
//2.Gradle执行: uploadArchivesToLocalRepo
//3.添加仓库: maven{ url 'file:C:/m2repository/repository'}
//4.添加依赖: compile 'sviolet:turquoise:version'

ext {
    mvn_temp_repo = rootProject.rootDir.absolutePath + '/release'
}

configure(subprojects.findAll { it.name.startsWith('turquoise') }) {

    apply plugin: 'maven'

    //编译时产生源代码jar
    task generateSourcesJar(type: Jar) {
        from android.sourceSets.main.java.srcDirs
        classifier 'sources'
    }

    //将源代码jar加入artifacts
    artifacts {
        archives generateSourcesJar
    }

    //上传artifacts
    uploadArchives {
        repositories.mavenDeployer {
            //暂时使用绝对路径指定本地仓库
            repository(url: uri(mvn_temp_repo))
            pom.project {
                groupId 'sviolet'
                artifactId project.name
                name project.name
                version mvn_version
                packaging 'aar'
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
                developers {
                    developer {
                        id 'shepherdviolet'
                        name 'Shepherd Violet'
                    }
                }
            }
        }
    }

}

//move archives to local repo
task uploadArchivesToLocalRepo(type: Copy, group: 'upload', description: 'move archives to local repo') {
    from uri(mvn_temp_repo)
    into uri(mvn_local_repo)
}
subprojects {
    tasks.getByName("uploadArchives") {
        uploadArchivesToLocalRepo.dependsOn it
    }
}


