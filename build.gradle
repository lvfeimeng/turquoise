
//构建脚本通用配置////////////////////////////////////////////////////////////////////////////////////

buildscript {

    //装载gradle-local.properties
    ext {
        gradleLocalProperties = new Properties()
        def gradleLocalFile = file("gradle-local.properties")
        if (gradleLocalFile.exists()) {
            gradleLocalFile.withInputStream { stream -> gradleLocalProperties.load(stream) }
        }

        upload_mavenVersion = gradleLocalProperties.upload_mavenVersion != null ? gradleLocalProperties.upload_mavenVersion : version_projectVersionName + '-SNAPSHOT'
        upload_repositoryPath = gradleLocalProperties.upload_repositoryPath != null  ? gradleLocalProperties.upload_repositoryPath : rootProject.rootDir.absolutePath + '/release'
        repository_localRepositoryPath = gradleLocalProperties.repository_localRepositoryPath
    }

    //构建脚本依赖仓库
    repositories {
        if (gradleLocalProperties.repository_localRepositoryPath != null) { maven {url gradleLocalProperties.repository_localRepositoryPath} }//local repo path

        jcenter()//jcenter
        maven { url 'https://jitpack.io' }
        maven { url 'https://github.com/shepherdviolet/alternate-maven-repository/raw/master/repository' }//custom repo
        maven { url 'https://maven.google.com' }
    }

    //构建脚本依赖
    dependencies {
        //common
        classpath 'com.android.tools.build:gradle:3.0.0-alpha3'//安卓构建插件
//        classpath 'com.android.tools.build:gradle:2.3.0'//安卓构建插件
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$version_kotlinVersion"//kotlin
        classpath "sviolet:gradle-turquoise:$version_gradleTurquoiseVersion"//gradle-turquoise plugin
    }

}

//全部工程通用配置////////////////////////////////////////////////////////////////////////////////////

allprojects {
    //工程依赖仓库
    repositories {
        if (gradleLocalProperties.repository_localRepositoryPath != null) { maven {url gradleLocalProperties.repository_localRepositoryPath} }//local repo path

        jcenter()//jcenter
        maven { url 'https://jitpack.io' }
        maven { url 'https://github.com/shepherdviolet/alternate-maven-repository/raw/master/repository' }//custom repo
        maven { url 'https://maven.google.com' }

//        mavenLocal()//本地仓库
//        maven{ url 'file:C:/m2repository/repository'}//本地仓库:指定路径
    }
}

//库工程通用配置////////////////////////////////////////////////////////////////////////////////////

configure(subprojects.findAll { it.name.startsWith('turquoise') }) {
    apply plugin: 'com.android.library'
    apply plugin: 'kotlin-android'

    android {
        compileSdkVersion 25
        buildToolsVersion '25.0.2'

        defaultConfig {
            minSdkVersion 14
            targetSdkVersion 25
            versionCode Integer.valueOf(version_projectVersionCode)
            versionName version_projectVersionName
        }

        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            }
        }
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$version_kotlinVersion"
    }

}

//DEMO工程通用配置////////////////////////////////////////////////////////////////////////////////////

configure(subprojects.findAll { it.name.startsWith('demo') }) {
    apply plugin: 'com.android.application'
    apply plugin: 'kotlin-android'

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$version_kotlinVersion"
    }

}

//其他////////////////////////////////////////////////////////////////////////////////////

//上传本地仓库
apply from: 'build-upload.gradle'
//工具任务
apply from: 'build-tools.gradle'
//应用gradle-turquoise插件
apply plugin: 'gradle-turquoise'
turquoise {
    printCachePathOfDependenciesFilter = ['compile']
}